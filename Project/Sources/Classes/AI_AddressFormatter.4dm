property systemPrompt : Text
property addressAsText : Text
property addressFormatterBot : cs.AIKit.OpenAIChatHelper
property responseData : Text
property UICallback_onProgress : 4D.Function
property UICallback_onTerminate : 4D.Function
property failedAttempts : Integer
property maxFailedAttempts : Integer
property startMillisecond : Integer
property timing : Integer


Class extends AI_Agent

singleton Class constructor()
	var $expectedSchema : Object
	
	Super()
	$expectedSchema:={streetNumber: "number"; streetName: "street name"; apartment: "number as string"; building: "building as string"; poBox: "po box as string"; city: "city as string"; region: "region as string"; postalCode: "postal code as string"; country: "country as string"}
	This.systemPrompt:="I need you to properly format an address as a structured object. "+\
		"I provide you an address as plain text and you must provide me a well-formatted json object corresponding to this address. "+\
		"The resulting address should be nicely and properly capitalized: first letter of each attribute, and names of streets "+\
		"The json formatted address must have the following json schema: "+JSON Stringify($expectedSchema)+". "+\
		"While  not all address attributes are mandatory, a general rule for an address to be valid is:\n"+\
		"*- streetName AND(streetNumber OR building OR poBox)\n"+\
		"*- city\n"+\
		"*- postalCode\n"+\
		"*- country"
	
	
Function onStreamChatReceive($result : cs.AIKit.OpenAIChatCompletionsResult)
	var $AIResponse : Object
	var $address : cs.address
	var $me:=cs.AI_AddressFormatter.me
	
	If ($result.success)
		If ($result.terminated)
			$me.UICallback_onProgress.call(Form; "\n\nAI response completed\n")
			$AIResponse:=$me.getAIStructuredResponseFromText($me.responseData; Is object)
			
			If ($AIResponse.success)
				$me.timing:=Milliseconds-$me.startMillisecond
				$address:=cs.address.new($AIResponse.response)
				$me.UICallback_onTerminate.call(Form; $address; $me.timing)
			Else   //bad format generated by AI
				$me.failedAttempts+=1
				$me.UICallback_onProgress.call(Form; "AI did not respond with the expected format.\n")
				If ($me.failedAttempts<$me.maxFailedAttempts)
					$me.prompt()
				End if 
			End if 
		Else   //$result.terminated = false
			$me.responseData+=$result.choice.delta.text
			$me.UICallback_onProgress.call(Form; $result.choice.delta.text)
		End if 
		
	Else   //$result.success = false
		throw(999; "Problem querying AI provider, please try again")
	End if 
	
Function prompt()
	var $prompt : Text
	
	$prompt:="Here is the address to format into json: <address> "+String(This.addressAsText)+"</address>. Start answering with ```json\n{"
	This.responseData:=""
	This.UICallback_onProgress.call(Form; "Prompt : "+$prompt+"\n\n")
	This.addressFormatterBot.prompt($prompt)
	
	
Function formatAddressAsync($addressAsText : Text; $callback : Object) : cs.address
	var $options : cs.AIKit.OpenAIChatCompletionsParameters
	
	This.failedAttempts:=0
	This.maxFailedAttempts:=3
	This.UICallback_onProgress:=$callback.onProgress
	This.UICallback_onTerminate:=$callback.onTerminate
	This.addressAsText:=$addressAsText
	
	$options:=cs.AIKit.OpenAIChatCompletionsParameters.new()
	$options.stream:=True
	$options.formula:=This.onStreamChatReceive
	$options.model:=This.model
	
	This.startMillisecond:=Milliseconds
	This.addressFormatterBot:=This.AIClient.chat.create(This.systemPrompt; $options)
	
	This.UICallback_onProgress.call(Form; "System prompt : "+This.systemPrompt+"\n\n")
	
	This.prompt()